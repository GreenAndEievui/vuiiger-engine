asm "
INCLUDE \"defines.inc\"
INCLUDE \"dungeon.inc\"
"

include "../include/script.evs";

script xGenerateScraper {
	u8 x = 32;
	u8 y = 32;

	repeat 255 {
		u8 dir;
		rand(dir);
		dir &= 3;

		step_direction(dir, x, y);
		map_put_tile(x, y, TILE_CLEAR);
	}

	map_put_tile(x, y, TILE_EXIT);
}

script xGenerateHalls {
	u8 x = 32;
	u8 y = 32;
	u8 direction;
	rand(direction);
	direction &= 3;

	u8 width;
	rand(width);
	width &= 3;
	width += 4;
	u8 height; 
	rand(height);
	height &= 3;
	height += 4;

	u8 start_offset;
	rand(start_offset);
	start_offset &= 3;
	x -= start_offset;
	rand(start_offset);
	start_offset &= 3;
	y -= start_offset;
	drop start_offset;
	
	for u8 i = 0; i != height; i += 1 {
		for u8 j = 0; j != width; j += 1 {
			map_put_tile(x, y, TILE_CLEAR);
			x += 1;
		}
		x -= width;
		y += 1;
	}

	drop i;
	drop j;

	repeat 9 {
		u8 hall_count;
		rand(hall_count);
		hall_count &= 1;
		hall_count += 2;

		while hall_count != 0 {
			u8 length;
			rand(length);
			length &= 7;
			length += 4;

			while length != 0 {
				map_put_tile(x, y, TILE_CLEAR);
				step_direction(direction, x, y);
				length -= 1;
			}

			u8 offset;
			rand(offset);
			// Set offset to either 2 or 0, then subtract 1 to get 1 or -1.
			offset &= 2;
			offset -= 1;
			// Add this to the current direction.
			direction += offset;
			direction &= 3;
			hall_count -= 1;
			drop length;
		}

		drop hall_count;

		u8 old_x = x;
		u8 old_y = y;

		rand(width);
		width &= 3;
		width += 3;
		rand(height);
		height &= 3;
		height += 3;
		
		for u8 i = 0; i != height; i += 1 {
			for u8 j = 0; j != width; j += 1 {
				map_put_tile(x, y, TILE_CLEAR);
				x += 1;
			}
			x -= width;
			y += 1;
		}

		x = old_x;
		y = old_y;
		
		drop i;
		drop j;
		drop old_x;
		drop old_y;

		if direction == UP {
			y -= 1;
		} else if direction == LEFT {
			x += width;
		} else if direction == DOWN {
			x += width;
			x -= 1;
			y += height;
		} else if direction == RIGHT {
			y += height;
			y -= 1;
			x -= 1;
		}
	}

	width = 3;
	height = 3;
	x -= 1;
	y -= 1;

	for u8 i = 0; i != height; i += 1 {
		for u8 j = 0; j != width; j += 1 {
			map_put_tile(x, y, TILE_CLEAR);
			x += 1;
		}
		x -= width;
		y += 1;
	}
	x += 1;
	y -= 2;
	map_put_tile(x, y, TILE_EXIT);
}

script xGenerateItems {
	do {
		u8 x;
		rand(x);
		x &= 63;
		u8 y;
		rand(y);
		y &= 63;
		
		u8 tile;
		map_get_tile(x, y, tile);
	} while tile != TILE_CLEAR

	// The items are given a weight in the following order:
	// ITEM0 - 6/16, 37.50% chance
	// ITEM1 - 6/16, 37.50% chance
	// ITEM2 - 3/16, 18.75% chance
	// ITEM3 - 1/16,  6.25% chance

	// Clamp the random number to (0, 15) and choose an item.
	u8 item_id;
	rand(item_id);
	item_id &= 15;
	if item_id < 6 { map_put_tile(x, y, TILE_ITEM0); }
	else if item_id < 12 { map_put_tile(x, y, TILE_ITEM1); }
	else if item_id < 15 { map_put_tile(x, y, TILE_ITEM2); }
	else { map_put_tile(x, y, TILE_ITEM3); }
}
