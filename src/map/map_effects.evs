include "src/include/script.evs";
#asm include "defines.inc" #end

script xDropletEffect {
	sprite droplet;

	droplet.x = 58;
	droplet.y = 24;
	droplet.tile = 126;
	droplet.attributes = 1;

	loop {
		repeat rand() & 127 {
			yield;
		}

		droplet.x = 59 + (rand() & 1);
		droplet.y = 24;

		draw_sprite(droplet);
		yield;
		yield;

		droplet.x = 60 + (rand() & 1);

		repeat 8 {
			droplet.y += 1;
			draw_sprite(droplet);
			yield;
			yield;
		}

		droplet.x = 59 + (rand() & 1);

		repeat 4 {
			droplet.y += 1;
			draw_sprite(droplet);
			yield;
			yield;
		}

		droplet.x = 58 + (rand() & 1);

		repeat 2 {
			droplet.y += 1;
			draw_sprite(droplet);
			yield;
			yield;
		}

		droplet.x = 57 + (rand() & 1);

		repeat 4 {
			droplet.y += 1;
			draw_sprite(droplet);
			yield;
			yield;
		}

		repeat 4 {
			droplet.y += 1;
			draw_sprite(droplet);
			yield;
			yield;
			yield;
		}

		repeat 4 {
			droplet.y += 1;
			draw_sprite(droplet);
			yield;
			yield;
			yield;
			yield;
		}
	}
}

script xDuckEffect {
	sprite duck;

	duck.x = 46;
	duck.y = 60;
	duck.tile = 124;

	loop {
		duck.attributes = 2;

		repeat 4 {
			repeat 4 {
				duck.x += 1;
				repeat 20 {
					draw_sprite(duck);
					yield;	
				}
			}
			duck.y += 1;
		}

		repeat 255 { draw_sprite(duck); yield; }

		duck.attributes = 34;

		repeat 4 {
			repeat 4 {
				duck.x -= 1;
				repeat 20 {
					draw_sprite(duck);
					yield;	
				}
			}
			duck.y -= 1;
		}

		repeat 255 { draw_sprite(duck); yield; }
	}
}

script xHazeEffect {
	if is_cgb() == 0 { return; }
	if get_flag(FLAG_CAVES_COMPLETE) == 0 { return; }

	// Check if the level has already been cleared.
	// We only show mist when the level is unlocked but not clear.
	if get_flag(FLAG_GEMTREE_COMPLETE) == 1 { return; }

	sprite pink_haze;
	sprite blue_haze;

	pink_haze.x = 141;
	pink_haze.y = 88;
	pink_haze.tile = 110;
	pink_haze.attributes = 68;

	blue_haze.x = 121;
	blue_haze.y = 84;
	blue_haze.tile = 116;
	blue_haze.attributes = 4;

	u8 direction = 1;
	u8 timer = 0;

	loop {
		repeat 8 {
			repeat 3 {
				draw_sprite(pink_haze);
				pink_haze.x += 8;
				pink_haze.tile += 2;
			}
			pink_haze.x -= 24;
			pink_haze.tile -= 6;
			repeat 3 {
				draw_sprite(blue_haze);
				blue_haze.x += 8;
				blue_haze.tile += 2;
			}
			blue_haze.x -= 24;
			blue_haze.tile -= 6;
			yield;
		}
		if timer < 16 {
			pink_haze.x -= direction;
			blue_haze.x += direction;
		} else if timer < 32 {
			pink_haze.x += direction;
			blue_haze.x -= direction;
		}

		if timer == 16 * 2 {
			timer = -1;
		}

		timer += 1;
	}
}
