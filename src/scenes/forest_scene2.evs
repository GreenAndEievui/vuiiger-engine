include "src/include/script.evs";
#asm
	include "entity.inc";
	include "scene.inc";
#end

npc xInitForestScene2 {
	if get_flag(FLAG_FOREST_COMPLETE) {
		open_map_immediately();
		return;
	}
	set_flag(FLAG_FOREST_COMPLETE, 1);

	load(xForestSceneBackground2);
	play_music(xCricketSound);

	// Luvui and Aris enter from the bottom of the screen
	// They both tiredly walk forwards, stopping the the middle of the field.
	position(PLAYER, 80, 96);
	position(PARTNER, 98, 96);
	frame(PLAYER, ENTITY_FRAME_STEP);
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 60 {
		dmove(PLAYER, UP, 8);
		dmove(PARTNER, UP, 8);
		yield;
	}
	frame(PLAYER, ENTITY_FRAME_IDLE);
	frame(PARTNER, ENTITY_FRAME_IDLE);
	sleep(40);
	direction(PLAYER, RIGHT);
	sleep(20);
	say(Luvui, "I'm… exhausted…");
	direction(PARTNER, LEFT);
	sleep(20);
	say(Aris, "…");


	// Luvui looks around.
	sleep(40);
	direction(PLAYER, LEFT);
	sleep(40);
	direction(PLAYER, DOWN);
	sleep(40);
	direction(PLAYER, LEFT);
	sleep(60);
	// She approaches the grass to their left.
	frame(PLAYER, ENTITY_FRAME_STEP);
	repeat 30 {
		dmove(PLAYER, LEFT, 12);
		yield;
	}
	// Aris falls asleep.
	frame(PARTNER, ENTITY_FRAME_SLEEP);
	special_frame(PARTNER);
	repeat 30 {
		dmove(PLAYER, LEFT, 12);
		yield;
	}
	frame(PLAYER, ENTITY_FRAME_IDLE);

	say(Luvui, "It's so late… <WAIT>they're probably gonna worry about us.");
	say(Aris, "…Zzz…");
	// Luvui turns to approach him
	direction(PLAYER, RIGHT);
	sleep(60);
	frame(PLAYER, ENTITY_FRAME_STEP);
	repeat 60 {
		dmove(PLAYER, RIGHT, 13);
		yield;
	}
	frame(PLAYER, ENTITY_FRAME_IDLE);
	// she gives him a few pokes…
	sleep(60);
	repeat 2 {
		repeat 5 {
			move(PLAYER, 16, 0);
			yield;
		}
		repeat 5 {
			move(PLAYER, -16, 0);
			yield;
		}
		sleep(30);
	}

	say(Luvui, "Come on, sleepy… <WAIT>it's not safe here…");
	frame(PLAYER, ENTITY_FRAME_STEP);
	repeat 20 {
		move(PLAYER, -4, 0);
		yield;
	}
	frame(PLAYER, ENTITY_FRAME_IDLE);
	sleep(30);

	// Luvui walks around Aris, looking at him from a few sides…
	u8 circle_distance = 40;

	frame(PLAYER, ENTITY_FRAME_STEP);
	repeat circle_distance - 10 {
		dmove(PLAYER, UP, 8);
		yield;
	}
	repeat circle_distance {
		dmove(PLAYER, RIGHT, 8);
		yield;
	}
	direction(PLAYER, DOWN);
	frame(PLAYER, ENTITY_FRAME_IDLE);
	sleep(60);

	frame(PLAYER, ENTITY_FRAME_STEP);
	repeat circle_distance {
		dmove(PLAYER, RIGHT, 8);
		yield;
	}
	repeat circle_distance - 10 {
		dmove(PLAYER, DOWN, 8);
		yield;
	}
	direction(PLAYER, LEFT);
	frame(PLAYER, ENTITY_FRAME_IDLE);
	sleep(60);

	frame(PLAYER, ENTITY_FRAME_STEP);
	repeat circle_distance - 10 {
		dmove(PLAYER, UP, 8);
		yield;
	}
	repeat circle_distance {
		dmove(PLAYER, LEFT, 8);
		yield;
	}
	direction(PLAYER, DOWN);
	frame(PLAYER, ENTITY_FRAME_IDLE);
	sleep(60);

	// …before pushing him offscreen.
	repeat 3 {
		frame(PLAYER, ENTITY_FRAME_STEP);
		repeat 10 {
			dmove(PLAYER, DOWN, 6);
			move(PARTNER, 0, 6);
			yield;
		}
		frame(PLAYER, ENTITY_FRAME_IDLE);
		sleep(60);
	}

	// HOUSE SCENE
	fade_to_black();
		sleep(120);
		load(xBarrelSceneBackground);
		play_music(xTownMusic);
		// init aris
		direction(PARTNER, RIGHT);
		frame(PARTNER, ENTITY_FRAME_IDLE);
		position(PARTNER, 88, 48);
		// init mom
		spawn_npc(2, xMom);
		frame(2, ENTITY_FRAME_IDLE);
		direction(2, UP);
		position(2, 0, 0);
		// init luvui
		position(PLAYER, 104, 48);
		direction(PLAYER, LEFT);
		yield; // Wait for the direction to change before switching to sleep.
		frame(PLAYER, ENTITY_FRAME_SLEEP);
		special_frame(PLAYER);
	fade_in();

	// Luvui is sleeping, with Aris standing over her.
	say(Aris, "…Luvui?<WAIT> Are you awake?");
	// Mom enters
	position(2, 72, 96);
	sleep(40);
	direction(PARTNER, DOWN);
	sleep(40);
	say(Mom, "You two were out very late last night.<WAIT> She was practically dragging you home!");
	direction(PARTNER, RIGHT);
	sleep(40);
	direction(PARTNER, DOWN);
	sleep(40);
	say(Aris, "I'm sorry… I just wanted to bring back lots of food.");

	// Mom approaches
	frame(2, ENTITY_FRAME_STEP);
	repeat 60 {
		dmove(2, UP, 12);
		yield;
	}
	frame(2, ENTITY_FRAME_IDLE);
	direction(PARTNER, LEFT);
	direction(2, RIGHT);
	sleep(40);

	say(Mom, "I have a feeling you were trying to impress her~♡");
	say(Aris, "Mom…");
	sleep(20);
	direction(PARTNER, DOWN);
	sleep(10);
	direction(PARTNER, RIGHT);
	sleep(40);
	direction(PARTNER, DOWN);
	sleep(10);
	direction(PARTNER, LEFT);
	sleep(40);
	say(Aris, "…she is cute.<WAIT> I hope she stays with us.");

	// Mom steps back to think.
	frame(2, ENTITY_FRAME_STEP);
	repeat 20 {
		dmove(2, LEFT, 4);
		yield;
	}
	frame(2, ENTITY_FRAME_IDLE);
	sleep(40);

	say(Mom, "You know how many of the animals here want to go back home.");
	direction(2, RIGHT);
	say(Mom, "Just don't be too disappointed if she's unhappy here.<WAIT> We'll take care of her for as long as she wants to stay, okay?");

	// Mom steps back to give him some space.
	frame(2, ENTITY_FRAME_STEP);
	repeat 40 {
		move(2, -4, 0);
		yield;
	}
	frame(2, ENTITY_FRAME_IDLE);
	sleep(40);

	say(Mom, "Let's let her sleep a little longer.<PAUSE> You should go donate the food you two collected.");
	say(Mom, "When she's awake, you can go play with her.<PAUSE> But promise me you'll come home before dark, okay?");
	// Aris steps forward
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 20 {
		dmove(PARTNER, LEFT, 8);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);
	say(Aris, "I promise.");
	// He turns to look at her again
	sleep(20);
	direction(PARTNER, DOWN);
	sleep(10);
	direction(PARTNER, RIGHT);
	sleep(40);
	direction(PARTNER, DOWN);
	sleep(10);
	direction(PARTNER, LEFT);
	sleep(40);
	say(Aris, "Tell her where to find me once she wakes up!");
	// he backs up
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 10 {
		move(PARTNER, 4, 0);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);
	// waits
	sleep(60);
	// and then runs off!
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 20 {
		dmove(PARTNER, DOWN, 16);
		yield;
	}
	direction(2, DOWN);
	repeat 25 {
		dmove(PARTNER, DOWN, 16);
		yield;
	}
	position(PARTNER, 0, 0);
	sleep(60);
	say(Mom, "He really likes her…");

	// TOWN SCENE
	fade_to_black();
		while check_fade() { yield; }
		load(xTreeSceneBackground);
		position(PLAYER, 0, 0);
		position(2, 0, 0);
		position(PARTNER, 14 * 8, 64);
		frame(PARTNER, ENTITY_FRAME_IDLE);
		direction(PARTNER, DOWN);
	fade_in();

	// Aris steps out of the house
	sleep(40);
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 20 {
		dmove(PARTNER, DOWN, 6);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);

	sleep(20);
	direction(PARTNER, RIGHT);
	sleep(40);
	direction(PARTNER, DOWN);
	sleep(10);
	direction(PARTNER, LEFT);
	sleep(40);

	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 120 {
		dmove(PARTNER, LEFT, 6);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);

	spawn_npc(2, xPlatypus); // This is just a placeholder NPC really.
	frame(2, ENTITY_FRAME_IDLE);
	direction(2, RIGHT);
	position(2, 56, 88);

	sleep(20);
	direction(PARTNER, DOWN);

	sleep(20);
	direction(2, UP);

	sleep(20);
	repeat 5 {
		dmove(2, UP, 12);
		yield;
	}

	say(Someone, "Hi, Aris!<WAIT> Where's your new friend?");
	say(Aris, "Luvui's still sleeping.<PAUSE> I'm just donating the food we collected yesterday.");
	say_no_wait(Someone, "Oh, that's nice.<PAUSE> Hey, I heard about something you might be interested in.");
	// Aris starts slowy walking away, thinking he's finished.
	sleep(80);
	repeat 20 {
		dmove(PARTNER, LEFT, 6);
		yield;
	}
	sleep(40);
	direction(PARTNER, DOWN);
	print_wait();
	say(Aris, "What's up?");
	say(Someone, "Well, you know those fields to the north of the forest?<WAIT> I heard that a couple of animals have been living out there, away from the village.");
	say(Someone, "Apparently they were looking for a way back home… Luvui might be interested in that.");
	say(Aris, "…");
	sleep(40);
	direction(PARTNER, DOWN);
	sleep(10);
	direction(PARTNER, LEFT);
	repeat 30 {
		dmove(PARTNER, LEFT, 4);
		yield;
	}
	say(Someone, "Aris…<WAIT> you can't hide the truth from her.<WAIT> If she wants to go home she should know.");
	repeat 20 {
		dmove(PARTNER, LEFT, 6);
		yield;
	}
	print_wait();
	sleep(60);
	say(Aris, "I know.<WAIT> I'll tell her.");

	repeat 30 {
		dmove(PARTNER, UP, 16);
		yield;
	}
	position(PARTNER, 0, 0);

	open_map();
}
