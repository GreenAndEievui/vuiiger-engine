include "src/scenes/scene.evs";

npc xInitForestScene {
	if get_flag(FLAG_SEEN_FOREST_CUTSCENE) {
		enter_dungeon_immediately(xForestDungeon_part3);
		return;
	}

	load(xForestSceneBackground);
	play_music(null);

	// Luvui and Aris enter from the left.
	// Luvui falls slightly behind, before stopping at the foot of a tree.
	position(PLAYER, 16, 48);
	position(PARTNER, 16, 64);
	frame(PLAYER, ENTITY_FRAME_STEP);
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 60 {
		dmove(PLAYER, RIGHT, 10);
		dmove(PARTNER, RIGHT, 12);
		yield;
	}
	frame(PLAYER, ENTITY_FRAME_IDLE);
	direction(PLAYER, UP);
	repeat 30 {
		dmove(PARTNER, RIGHT, 12);
		yield;
	}

	// Aris notices and slows down before turning.
	repeat 30 {
		dmove(PARTNER, RIGHT, 8);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);
	direction(PARTNER, LEFT);
	sleep(60);

	say(Luvui, "We've gone out pretty far.<WAIT> The forest is getting a lot thicker.");

	// Aris walks over to Luvui to look at the trees.
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 32 {
		dmove(PARTNER, LEFT, 12);
		yield;
	}
	repeat 20 {
		dmove(PARTNER, UP, 12);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);

	// Short delay before speaking.
	sleep(120);
	say(Aris, "You're rightâ€¦ it's getting dark too.");
	sleep(120);
	direction(PARTNER, LEFT);
	sleep(30);
	direction(PLAYER, RIGHT);
	sleep(30);
	say(Aris, "There's a little grotto with lots of fruit up ahead.<WAIT> Let's go a bit further before we head back.");
	sleep(30);

	// Exit scene while fading out.
	frame(PLAYER, ENTITY_FRAME_STEP);
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 30 {
		dmove(PLAYER, RIGHT, 14);
		dmove(PARTNER, RIGHT, 14);
		yield;
	}
	set_flag(FLAG_SEEN_FOREST_CUTSCENE, 1);
	enter_dungeon(xForestDungeon_part3);
	repeat 30 {
		dmove(PLAYER, RIGHT, 14);
		dmove(PARTNER, RIGHT, 14);
		yield;
	}
}
