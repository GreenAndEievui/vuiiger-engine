include "src/include/script.evs";
#asm
	include "entity.inc";
	include "scene.inc";
#end

npc xInitForestScene {
	if get_flag(FLAG_SEEN_FOREST_CUTSCENE) {
		enter_dungeon_immediately(xForestDungeon_part3);
	}

	load(xForestSceneBackground);

	// Luvui and Aris enter from the left.
	// Luvui falls slightly behind, before stopping at the foot of a tree.
	position(PLAYER, 16, 48);
	position(PARTNER, 16, 64);
	frame(PLAYER, ENTITY_FRAME_STEP);
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 60 {
		dmove(PLAYER, RIGHT, 10);
		dmove(PARTNER, RIGHT, 12);
		yield;
	}
	frame(PLAYER, ENTITY_FRAME_IDLE);
	direction(PLAYER, UP);
	repeat 30 {
		dmove(PARTNER, RIGHT, 12);
		yield;
	}

	// Aris notices and slows down before turning.
	repeat 30 {
		dmove(PARTNER, RIGHT, 8);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);
	direction(PARTNER, LEFT);
	repeat 60 { yield; }

	say("Luvui", "We've gone out pretty far.<WAIT> The forest is getting a lot thicker.");

	// Aris walks over to Luvui to look at the trees.
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 32 {
		dmove(PARTNER, LEFT, 12);
		yield;
	}
	repeat 20 {
		dmove(PARTNER, UP, 12);
		yield;
	}
	frame(PARTNER, ENTITY_FRAME_IDLE);

	// Short delay before speaking.
	repeat 120 { yield; }
	// TODO: map unicode ellipsis (â€¦) to this delay pattern
	say("Aris", "You're right<SET_DELAY><5>...<SET_DELAY><2><DELAY><30> it's getting dark too.");
	repeat 120 { yield; }
	direction(PARTNER, LEFT);
	say("Aris", "There should be a few more apples up ahead.<WAIT> Let's go a bit further before we head back.");
	direction(PLAYER, RIGHT);
	repeat 30 { yield; }

	// Exit scene while fading out.
	frame(PLAYER, ENTITY_FRAME_STEP);
	frame(PARTNER, ENTITY_FRAME_STEP);
	repeat 30 {
		dmove(PLAYER, RIGHT, 14);
		dmove(PARTNER, RIGHT, 14);
		yield;
	}
	set_flag(FLAG_SEEN_FOREST_CUTSCENE, 1);
	enter_dungeon(xForestDungeon_part3);
	repeat 30 {
		dmove(PLAYER, RIGHT, 14);
		dmove(PARTNER, RIGHT, 14);
		yield;
	}
}
